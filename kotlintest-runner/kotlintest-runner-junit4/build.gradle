plugins {
    id 'java'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'java-library'
}

repositories {
    mavenCentral()
}

kotlin {

    jvm()
    js()

    sourceSets {

        commonMain {
            dependencies {
                api project(":kotlintest-assertions")
                implementation project(':kotlintest-core')
                implementation kotlin("stdlib-common")
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation project(':kotlintest-runner:kotlintest-runner-jvm')
                implementation 'junit:junit:4.12'
                implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'
                implementation 'com.github.ajalt:mordant:1.2.1'
            }
        }

        jvmTest {
            dependencies {
                implementation project(':kotlintest-runner:kotlintest-runner-junit5')
                implementation 'org.mockito:mockito-core:2.24.0'
                implementation "com.nhaarman:mockito-kotlin:1.6.0"
            }
        }
    }
}

compileKotlinJs.configure {
    kotlinOptions {
        moduleKind = 'commonjs'
    }
}

compileTestKotlinJs.configure {
    kotlinOptions {
        moduleKind = 'commonjs'
    }
}

compileKotlinJvm {
    kotlinOptions {
        freeCompilerArgs += '-Xuse-experimental=kotlin.Experimental'
    }
}

test {
    exclude 'com/sksamuel/kotlintest/runner/junit4/samples/**'

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}

apply from: '../../publish.gradle'
